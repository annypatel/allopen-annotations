buildscript {

    // Build configuration
    ext.buildConfig = [
        'minSdk'    : 16,
        'compileSdk': 28,
        'targetSdk' : 28
    ]

    // Dependency versions
    ext.versions = [
        'agp'     : '3.3.0-beta04',
        'kotlin'  : '1.3.10',
        'spotless': '3.16.0',
        'ktlint'  : '0.29.0',
        'gvp'     : '0.20.0',
        'amp'     : '3.6.2',
        'dokka'   : '0.9.17',
        'lint'    : '26.2.1',
        'junit'   : '4.12'
    ]

    // Dependencies
    ext.deps = [
        'kotlin': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin",
        'lint'  : [
            'api' : "com.android.tools.lint:lint-api:$versions.lint",
            'test': "com.android.tools.lint:lint-tests:$versions.lint"
        ],
        'junit' : "junit:junit:$versions.junit"
    ]

    repositories {
        google()
        jcenter()

    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.agp"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$versions.kotlin"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$versions.spotless"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gvp"
        classpath "digital.wup:android-maven-publish:$versions.amp"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$versions.dokka"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.diffplug.gradle.spotless'

    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
        }
    }

    plugins.withId("kotlin") {

        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    plugins.withId("com.android.library") {

        android {
            compileOptions {
                sourceCompatibility = "1.8"
                targetCompatibility = "1.8"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
